plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

group = 'com.dnsimple'
version = '0.9.0'
description = 'dnsimple-java'
sourceCompatibility = '1.11'
targetCompatibility = '1.11'

java {
    withJavadocJar()
    withSourcesJar()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = description
            version = version

            from components.java

            pom {
                name = 'dnsimple-java'
                description = 'DNSimple Java Bindings'
                url = 'https://github.com/dnsimple/dnsimple-java'
                properties = [:]
                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://choosealicense.com/licenses/mit/'
                    }
                }
                organization {
                    name = 'DNSimple'
                    url = 'https://dnsimple.com'
                }
                developers {
                    developer {
                        name = 'DNSimple'
                        email = 'support@dnsimple.com'
                        organization = 'DNSimple'
                        organizationUrl = 'https://dnsimple.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dnsimple/dnsimple-java.git'
                    developerConnection = 'scm:git:git@github.com:dnsimple/dnsimple-java.git'
                    url = 'git@github.com:dnsimple/dnsimple-java.git'
                }
            }

            repositories {
                maven {
                    def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
                    url = isReleaseVersion ? releaseRepo : snapshotRepo
                    credentials {
                        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

